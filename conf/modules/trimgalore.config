process {
    withName: TRIMGALORE {
        ext.args = { [
            // Static args
            '--fastqc',

            // Special flags
            params.rrbs ? '--rrbs' : '',
            params.nextseq_trim > 0 ? "--nextseq ${params.nextseq_trim}" : '',
            params.length_trim ? "--length ${params.length_trim}" : '',

            // Trimming - R1
            params.clip_r1 > 0 ? "--clip_r1 ${params.clip_r1}" : (
                params.skip_trimming_presets ? '' : (
                    params.pbat ? "--clip_r1 8" : (
                        params.single_cell ? "--clip_r1 6" : (
                            (params.accel || params.zymo || params.em_seq) ? "--clip_r1 10" : ''
                        )
                    )
                )
            ),

            // Trimming - R2
            meta.single_end ? '' : (
                params.clip_r2 > 0 ? "--clip_r2 ${params.clip_r2}" : (
                    params.skip_trimming_presets ? '' : (
                        params.pbat ? "--clip_r2 8" : (
                            params.single_cell ? "--clip_r2 6" : (
                                (params.zymo || params.em_seq) ? "--clip_r2 10" : (
                                        params.accel ? "--clip_r2 15" : ''
                                )
                            )
                        )
                    )
                )    
            ),

            // Trimming - 3' R1
            params.three_prime_clip_r1 > 0 ? "--three_prime_clip_r1 ${params.three_prime_clip_r1}" : (
                params.skip_trimming_presets ? '' : (
                    params.pbat ? "--three_prime_clip_r1 8" : (
                        params.single_cell ? "--three_prime_clip_r1 6" : (
                            (params.accel || params.zymo || params.em_seq) ? "--three_prime_clip_r1 10" : ''
                        )    
                    )               
                )
            ),

            // Trimming - 3' R2
            meta.single_end ? '' : (
                params.three_prime_clip_r2 > 0 ? "--three_prime_clip_r2 ${params.three_prime_clip_r2}" : (
                    params.skip_trimming_presets ? '' : (
                        params.pbat ? "--three_prime_clip_r2 8" : (
                            params.single_cell ? "--three_prime_clip_r2 6" : (
                                (params.accel || params.zymo || params.em_seq) ? "--three_prime_clip_r2 10" : ''
                            )
                        )
                    )
                )
            ),
        ].join(' ').trim() }
        publishDir = [
            [
                path: { "${params.outdir}/trimgalore/fastqc" },
                mode: params.publish_dir_mode,
                pattern: "*.html"
            ],
            [
                path: { "${params.outdir}/trimgalore/fastqc/zips" },
                mode: params.publish_dir_mode,
                pattern: "*.zip"
            ],
            [
                path: { "${params.outdir}/trimgalore" },
                mode: params.publish_dir_mode,
                pattern: "*.fq.gz",
                enabled: params.save_trimmed
            ],
            [
                path: { "${params.outdir}/trimgalore/logs" },
                mode: params.publish_dir_mode,
                pattern: "*.txt"
            ]
        ]
    }
}
